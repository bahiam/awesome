/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 LinkÃ¶ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome;
use cm.syntax;


public syntax str->Object aweConsumerContractItem {
    
    //method
    @returnType=type @name=id @args=formalArgList { 
        str->Object data();
        data.put("contractType", "method");
        data.put("returnType", returnType);
        data.put("name", name);
        data.put("args", args);
        return data;
    }

    //values
    @returnType=type @name=id {
        str->Object data();
        data.put("contractType", "prop");
        data.put("returnType", returnType);
        data.put("name", name);
        return data;
    }

}

public member @visibility contract '(' @contractItems=[aweConsumerContractItem ',']* @lastContractItem=aweConsumerContractItem ')' '{' @expressions=[statement]* '}'  {
    
    contractItems << lastContractItem;


    SMembers props(noSrcRef);
    SMembers methods(noSrcRef);
    SFormalArgList constructorParams(noSrcRef);

    constructorParams << SFormalArg(SType("AweObject"), SId("source"));

    for(item in contractItems) {

        //props
        if(item.get("contractType").str == "prop") {
            SId propName = item.get("name").SId;
            str propNameStr = propName.name;
            SType propType = item.get("returnType").SType;

            props << member {
                extend @visibility @propType @propName() {
                    return this.__source.?get(@propNameStr).@propType;
                }
            };
        }

        //methods
         if(item.get("contractType").str == "method") {
            SId methodName = item.get("name").SId;
            str methodNameStr = methodName.name;
            SType methodReturnType = item.get("returnType").SType;
            SFormalArgList methodArgs = item.get("args").SFormalArgList;
            SExpr zero = methodReturnType.type().literal0();

            SActualArgList actualArgs();

            for(i in 0..(methodArgs.count-1)) {
                SFormalArg arg = methodArgs[i];
                SActualArg actualArg(arg.id.name);
                actualArgs << actualArg;
            }

            if(SType("void").equal(methodReturnType)) {
                methods << member {
                    extend @visibility @methodReturnType @methodName(@methodArgs) {
                        if(this.__source)  {
                            dynamic_call(__source.@methodName(@actualArgs));
                        }
                    }
                };
            } else {
                methods << member {
                    extend @visibility @methodReturnType @methodName(@methodArgs) {
                        if(!this.__source) return @zero;
                        return dynamic_call(__source.@methodName(@actualArgs):@methodReturnType);
                    }
                };
            }

            
        }
    }
    SStatements s = statement {};
    for(e in expressions) {
        s << e;
    }

    pln(#s);

    return member {
        @visibility AweObject __source;

        @visibility constructor(@constructorParams) {
            this.__source = source;
            @s;
        }

        @props;

        @methods;

    };
}

public aweObject MySource {
    props(
        double width = 52inch,
        double height = 48inch,
        AweObject child = null
    );

    public constructor() {
        pln("hello!");
    }

    extend public void sayHi() {
        pln("hi!");
    }

    extend public double plusOne(double x) {
        return x + 1;
    }
}

public class MySourceConsumer {
    public contract(
            double width,
            double height,
            AweObject child,
            void sayHi(),
            double plusOne(double x)
    )
    {
        pln("hi there");
    }
    
}

{
    MySource s();
    MySourceConsumer c(s);
    pln(#c.width; #c.child);
    c.sayHi();
    pln(#c.plusOne(7));
}
