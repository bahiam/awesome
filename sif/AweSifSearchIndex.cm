/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Linköping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.sif;

public class AweSifSearchIndex {
    public AweSifCatalog source;

    public str->str{} basemodelIndex();
    public str->str{} descriptionIndex();

    public constructor(AweSifCatalog source) {
        this.source = source;
        this.process();
    }   

    extend public void process() {
        for(modelStr in this.source.allBaseModels()) {
            AweSifModel model = this.source.model(modelStr);
            this.process(model);
        }
    }

    extend public void process(AweSifModel basemodel) {
        this.processCode(basemodel.code);
        this.processDescrition(basemodel.code, basemodel.description);
    }

    extend public str{} modelList(str indexKey, str->str{} index) {
        var list = index.get(indexKey);
        if(!list) {
            list = new str{}();
            index.put(indexKey, list);
        }
        return list;
    }

    extend public void processCode(str code) {
        var tokens = this.tokenizeBasemodel(code);
        for(token in tokens) {
            var list = modelList(token, this.basemodelIndex);
            list << code;
        }
    }

    extend public void processDescrition(str code, str description) {
        var tokens = this.tokenizeDescription(description);
        for(token in tokens) {
            var list = modelList(token, this.descriptionIndex);
            list << code;
        }
    }

    extend public AweSifModel[] search(str code="", str description="") {
        str{} modelsFound();
        
        if(!description.emptyOrOnlyWhiteSpace) {
            var descriptionTokens = this.tokenizeDescription(description);
            for(token in descriptionTokens) {
                var models = this.descriptionIndex.get(token);
                if(modelsFound.empty) {
                    modelsFound = models;
                } else {
                    modelsFound = models * modelsFound;
                }
            }
        }

        if(!code.emptyOrOnlyWhiteSpace) {
            var models = this.basemodelIndex.get(this.cleanBasemodel(code));
            if(modelsFound.empty) {
                modelsFound = models;
            } else {
                modelsFound = models * modelsFound;
            }
        }

        AweSifModel{} found();
        for(m in modelsFound) {
            found << this.source.model(m);
        }
        return found.seq;
    }

    extend public str{} tokenizeBasemodel(str basemodel) {
        basemodel = this.cleanBasemodel(basemodel);
        
        str{} tokens();
        tokens << basemodel;
    
        for(i in (2..basemodel.count)) {
            tokens << basemodel.substring(0,i);
        }
        return tokens;
    }

    extend public str cleanBasemodel(str basemodel) {
        StrBuf clean();
        for(c in basemodel) {
            if(c.isLetter) {
                clean << c.toLower();
            }
        }
        return clean.toS;
    }
    
    extend public str{} tokenizeDescription(str description) {
        str{} tokens();
        description = description.toLower();
        CmScanner scanner(description);
        var allTokens = scanner.tokenize(" /-,.");
        for(token in allTokens) {
            if(token.count > 1 and token.Double == null) {
                tokens << token;
            }
        }
        return tokens;
    }
}



{
    // pln(#tokenizeBasemodel("abcdefg"));
    // pln(#tokenizeDescription("Trooper LT/íAdj Arms w/CTB133 disc 7/3/16"));
    AweSifCatalog seatingCatalog("ALC", 
        Url("C:\\Users\\User\\Downloads\\ALC.1"),
        Url("C:\\Users\\User\\Downloads\\ALC.2"),
        Url("C:\\Users\\User\\Downloads\\ALC.3")
    );
    AweSifSearchIndex search(seatingCatalog);
    var found = search.search("MIWKANO", "Work Chair knit");
    for(f in found) {
        pln(#f);
    }
}