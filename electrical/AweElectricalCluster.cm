/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.electrical;

public class AweElectricalCluster {
    
    public AweElectricalInfeedPoint[] infeeds();
    public AweElectricalCableLine[] cables();

    public constructor() {
    }   

    extend public point{} allVertices(bool clickableOnly=false) {
        point{} allVertices();
        for(l in spaceLines(clickableOnly)) {
            if (l.clickableP0) allVertices << l.p0;
            if (l.clickableP1) allVertices << l.p1;
        }

        return allVertices;
    }

    extend private AweElectricalLine[] spaceLines(bool clickableOnly=false) {
        AweElectricalLine[] lines();

        AweElectricalCableLine[] allCables();
        
        allCables +=? cables;

        for (infeed in infeeds) {
            allCables +=? infeed.cables;
        }

        for(c in allCables) if (!clickableOnly or c.clickable) {
            lines << AweElectricalLine(c.spaceLine.v, c.clickableP0, c.clickableP1);
        }
        
        return lines;
    }

    extend public WeightedLine[] weightedSpaceLines(bool clickableOnly=false) {
        WeightedLine[] lines();

        AweElectricalCableLine[] allCables();
        
        allCables +=? cables;

        for (infeed in infeeds) {
            allCables +=? infeed.cables;
        }

        for(c in allCables) if (!clickableOnly or c.clickable) {
            lines << WeightedLine( c.spaceLine.v, c.weight );
        }
        
        return lines;
    }
}

private class AweElectricalLine extends Line {
    public bool clickableP0;
    public bool clickableP1;

    public constructor(line l, bool clickableP0=true, bool clickableP1=true) {
        set*(this: clickableP0, clickableP1);

        super(l);
    }

    extend public point p0() {
        return this.v.p0;
    }

    extend public point p1() {
        return this.v.p1;
    }
}