/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome.electrical;

public class AweElectricalComponent : abstract {
    public constructor() {} 

    public str id;
    public AweElectricalComponent upstream;
    public AweElectricalComponent{} downstream();
    public bool hideInCluster3D;
    
    extend public AweElectricalComponent root() {
        var root = this.upstream;
        
        if (!root) return this;

        AweElectricalComponent{} visited();

        while (root.upstream) {
            if (root.upstream in visited) break;

            visited << root.upstream;
            root = root.upstream;
        }

        return root;
    }

    extend public bool hasComponentInStream(str componentId) {
        return this.id == componentId or this.hasDescendant(componentId) or this.hasAncestor(componentId);
    }

    extend public bool hasDescendant(str descendantId, AweElectricalComponent{} visited=null) {
        var downstream = this.downstream;
        
        if (!visited) visited = new AweElectricalComponent{}();

        for (component in downstream) {
            if (component in visited) return false;

            if (component.id == descendantId) return true;

            if (component.hasDescendant(descendantId, visited)) {
                return true;
            }

            visited << component;
        }

        return false;
    }

    extend public bool hasAncestor(str ancestorId) {
        var upstream = this.upstream;
        
        if (!root) return false;

        AweElectricalComponent{} visited();

        while (upstream) {
            if (upstream in visited) break;

            if (upstream.id == ancestorId) return true;

            visited << upstream;

            upstream = upstream.upstream;
        }

        return false;
    }

    public AweNotifyProp bool active {
        this.activeChanged();
    }
    
    extend public void resetStreamInfo() {
        this.upstream = null;
        this.downstream = new AweElectricalComponent{} ();
    }

    //in space coordinates
    extend public point[] vertices() : abstract { }
}