/** Configura CET Source Copyright Notice (CETSC)

   This file contains Configura CM source code and is part of the
   Configura CET Development Platform (CETDEV). Configura CM
   is a programming language created by Configura Sverige AB.
   Configura, Configura CET and Configura CM are trademarks of
   Configura Sverige AB. Configura Sverige AB owns Configura CET,
   Configura CM, and CETDEV.

   Copyright (C) 2004 Configura Sverige AB, All rights reserved.

   You can modify this source file under the terms of the Configura CET
   Source Licence Agreement (CETSL) as published by Configura Sverige AB.

   Configura Sverige AB has exclusive rights to all changes, modifications,
   and corrections of this source file. Configura Sverige AB has exclusive
   rights to any new source file containing material from this source file.
   A new source file based on this source file or containing material from
   this source file has to include this Configura CET Source Copyright Notice
   in its full content. All changes, modifications, and corrections mentioned
   above shall be reported to Configura Sverige AB within One Month from
   the date that the modification occurred.

   Configura CM is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   See the CETSL for more details.

   You should have received a copy of the CETSL along with the CETDEV.
   If not, write to Configura Sverige AB, Box 306, SE-581 02 Link√∂ping, Sweden.
   Tel +46 13377800, Fax +46 13377855,
   Email: info@configura.com, www.configura.com

   END OF CETSC
*/

package custom.awesome;
use cm.syntax;

public class AweContract {
    
    private Object core;
    public constructor auto();

}

public definition @visibility contract @name=id '{' @members=[classMember]* '}' {

    SMembers membersBundle(noSrcRef);
    for(m in members) {
        membersBundle << m;
    }

    definition {
        @visibility class @name extends AweContract {
            @membersBundle;
        }
    };
}

public member @visibility method @t=type @name=id @args=formalArgList ';' {
    SActualArgList actualArgs();

    for(i in 0..(args.count-1)) {
        SFormalArg arg = args[i];
        SActualArg actualArg(arg.id.name);
        actualArgs << actualArg;
    }

    SExpr zero = t.type().literal0();

    if(SType("void").equal(t)) {
        return member {
            extend @visibility @t @name(@args) {
                if(!core) return;
                dynamic_call(core.@name(@actualArgs));
            }
        };
    } else {
        return member {
            extend @visibility @t @name(@args) {
                if(!core) return @zero;
                return dynamic_call(core.@name(@actualArgs):@t);
            }
        };
    }

}

public member @visibility property @t=type @name=id ';' {
    str nameStr = name.name;
    SExpr zero = t.type().literal0();
    return member {
        extend @visibility @t @name() {
            for(field in this.core.class.fields) {
                if(field.name == @nameStr) {
                    return this.core.get(field).@t;
                }
            }
            return @zero;
        }

        extend @visibility @t @name=(@t newValue) {
            for(field in this.core.class.fields) {
                if(field.name == @nameStr) {
                    this.core.set(field, newValue);
                }
            }
            return newValue;
        }
    };
}

public member @visibility implements @t=type ';' {
    str name = t.name;
    return member {
        extend @visibility @t @name() {
            return new @t(this);
        }
    };
}


/********** example ****************/
// public contract IWideThing {
//     public property double width;
//     public method double calculateArea(double depth);
// }

// public class Thing {
//     public implements IWideThing;
//     public constructor() {}
//     public double width = 5;
//     extend public double calculateArea(double depth) {
//         return this.width * depth;
//     }
// }

// public void printArea(IWideThing thing) {
//     //can set it too...
//     thing.width = 7;
//     pln("The area is: ", thing.calculateArea(3));
// }

// {
//     Thing thing();
//     printArea(thing.IWideThing);
    
// }

